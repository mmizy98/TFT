label tft_prolog:

    $ new_chapter(0, u"Судьбы Двух. Пролог")
    $ persistent.timeofday = "night"
    $ prolog_time()

    window hide
    stop ambience
    play sound porn_diapazon fadein 3
    scene int_prolog_nightclub with dissolve2
    pause (11.5)
    play sound claps fadein 1
    play music stressedoutminus fadein 10
    window show

    "Концерт окончился, зал заполся аплодисментами."
    "Поблагодарив слушателей, мы собрали свои инструменты и ушли за кулисы."
    "Сегодня всё обошлось без происшествий, как в прошлый раз, но вымотался я по ощущению сильнее."
    "Однако мои товарищи и по совместительству коллеги по работе выглядели бодро."
    muz "Дружище, мы тут выпить решили, пойдешь с нами? Здесь буквально через дорогу отличный бар есть."
    "После выступления я сильно устал, но и отказываться выпить с друзьями было бы как-то не правильно. {w}Как же поступить?"

    window hide
    menu:
        "Согласиться":

            $ day0_drunk =  True

            window show
            "Желание необидеть товарищей победило, и я всё-же согласился."
            dns "Знаешь, а почему бы и нет?"
            muz "Знал что ты не откажешься!"
            dns "А то!"
            "Улыбаясь произнёс я."

            window hide
            scene black with flash_black
            show aftertwohour with dissolve2
            pause (1)
            scene black with dissolve2
            pause (2)
            scene night_bar with dissolve2
            window show

            muz "Ну что,{w=0.5} выпьем за нашу{w=0.5} группу!"
            muzs "Выпьем!"
            dns "*ИК*{w=0.5} Выпьем!"
            
            window hide
            pause (1)
            window show

            "После очередного шота меня совсем разнесло."
            dns "Ладно парни я наверное {w=0.5}*ИК* {w=0.5}домой пойду короче."
            muz "Давай братишка, {w=0.5}удачи!"
            dns "Удачи!"
            "Я взял со стола еще не открытую бутылку виски и пока никто не видел убрал её в рюкзак. {w}Одел пальто и вышел из бара."

            window hide
            
        "Отказаться":

            window show
            $ day0_drunk =  False

            dns "Нет уж, замотался я что-то."
            muz "Ну как хочешь. {w}Давай, еще увидимся!"
            dns "Да, удачи."
            "Мы пожали руки. {w}Я развернулся и собирался уходить, но передомной вдруг выросла чья-то фигура."
            fan "Извините, можно вас?"
            dns "Да, что случилось?"
            fan "Знаете, мне очень нравится ваша музыка, и в благодарность за ваше творчество  мне бы хотелось сделать вам небольшой презент."
            fan "Моя семья занимается купажом, и мы были бы очень рады если бы вы попробовали один из наших напитков."
            "Он протянул мне бутылку виски."
            dns "Большое спасибо, обязательно попробую."
            "Мужчина судя по всему хотел подойти еще к кому-то из участников группы."
            "Я не стал ему мешать, собрал вещи и направился домой."

            window hide

    scene black with dissolve2
    play sound ambience_city fadein 3
    play sound_loop sfx_far_steps fadein 3
    show ext_winterpark_7dl at stepping with dissolve2
    window show

    if day0_drunk:
        "Шагая ночью по пустому Петербургу в мою голову приходило множество мыслей, но невозможно было выцепить какую-то одну."
    else:
        "Шагая ночью по пустому Петербургу в мою голову приходило множество мыслей, но невозможно было выцепить какую-то одну."
    "Все они будто были неуловимы, отчего даже становилось немного не по себе."
    if day0_drunk:
        "Но мороз делал своё дело и я чувствовал как потихоньку трезвел."
    else:
        "Но мороз делал своё дело и я чувствовал как потихоньку замерзал."
    "На секунду промелькнула идея прогуляться по волшебному ночному городу, всё-равно ждать меня дома некому."
    "Родители далеко, все друзья уже давно спят, девушки нет."
    "Не одинокий, но вполне счастливый человек."
    "Для кого-то даже одиночество - приятная штука. {w}А кому-то тяжело быть одиноким."
    "Я не отношусь ни к первым ни ко вторым."
    "Конечно можно было бы найти себе близкого человека, но я из тех людей, которые считают что все должно произойти само собой."
    "''Один - не значит одинок.'' {w}Гениальная фраза. {w=1}И кто её только придумал?"

    window hide
    pause (1)
    scene black with dissolve2
    show ulica2 at stepping with dissolve2
    window show

    "В раздумьях я успел пройти мимо нужного мне пешеходного перехода."
    "Возвращаться было лень, да и какой человек стал бы так делать."
    if day0_drunk:
        "Я посмотрел в обе стороны пьяным, но от того не менее серьезным взглядом. {w}Дорога была пуста."
    else:
        "Я посмотрел в обе стороны. {w}Дорога была пуста."
    th "Машин нет, значит можно переходить."

    play sound_loop sfx_far_steps fadein 2

    "Аккуратно сделав шаг вперёд, дабы не навернуться на гололёде, я оказался на проезжей части."

    stop music fadeout 4

    "Оставалось лишь пройти до конца."
    if day0_drunk:
        "Задача дополнительно усложнялась действием алкоголя."
    "Шаг за шагом, и вот я уже на середине абсолютно пустой дороги."
            
    window hide
    play sound avaria volume 1.35
    pause (1.5)
    scene black with dissolve2
    pause (1)
    play sound_loop sfx_head_heartbeat fadein 7
    play music Omelchuk_Roads_Ahead fadein 3
    scene anim prolog_1 with dissolve2
    pause (3.5)

    "Звук тормозов не пойми откуда взявшейся машины - последнее что я успел услышать."
    "Ни удара, ни боли. {w}Ни-че-го."
    "За ним последовала невероятная лёгкость, будто невесомость, как в астрале."
    "Я не чувствовал своего тела, лишь только громкое сердцебиение раздовалось по всему сознанию."
    "Единственное что я точно знал - что просто хочу вернуться назад."
    "Это всё что приходило на ум в тот момент."
    "В момент, когда не понимаешь что с тобой происходит и жив ли ты вообще."

    window hide
    pause (2)
    scene black with Dissolve(4)
    stop music fadeout 6
    pause (4)
    stop ambience fadeout 7
    stop sound_loop fadeout 7
    pause (6)
    jump tft_day1